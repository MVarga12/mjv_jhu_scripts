#small toy model to run a system of CLTC, EPN1, and L
#for Dr. Johnson's Nov '17 grant

begin model
begin parameters
	NA 6.022e23 # mol^-1
	vol_cp 1.6E-14
	sa = 0.5*vol_cp
	sigma 0.002 #um
	vol_pm = sa*sigma
	conc_cltc = 0.1*1E-6 #M
	conc_epn1 = 1*1E-6 #M
	
	#binding - convert uM to M, divide by NA
	koff 				1
	kon_cltc_cltc		(1/(100*1E-6))/NA
	kon_cltc_epn1		(1/(22*1E-6))/NA
	kon_epn1_l			(1/(0.25*1E-6))/NA
	
	kon_cltc_cltc_cp	kon_cltc_cltc/vol_cp
	kon_cltc_epn1_cp	kon_cltc_epn1/vol_cp
	kon_epn1_l_cp		kon_epn1_l/vol_cp
	
	kon_cltc_cltc_pm	kon_cltc_cltc/vol_pm - kon_cltc_cltc_cp
	kon_cltc_epn1_pm	kon_cltc_epn1/vol_pm - kon_cltc_epn1_cp
	kon_epn1_l_pm		kon_epn1_l/vol_pm - kon_epn1_l_cp
	
	CLTC_0				conc_cltc*vol_cp*NA
	EPN1_0				conc_epn1*vol_cp*NA
	L_0					2.5E4 * sa / 1E-15
end parameters

begin molecule types
	CLTC(coat,coat,coat,epn1,epn1,epn1)
	EPN1(coat,L)
	L(p)
end molecule types

begin seed species
	CLTC(coat,coat,coat,epn1,epn1,epn1) CLTC_0
	EPN1(coat,L) 	EPN1_0
	L(p)			L_0
end seed species

begin observables
	Molecules cltc_l CLTC().L()
	Molecules epn1_l EPN1().L() 
end observables

begin reaction rules
	CLTC(coat) + CLTC(coat) <-> CLTC(coat!1).CLTC(coat!1) kon_cltc_cltc_cp, koff
	CLTC(coat).L() + CLTC(coat).L() -> CLTC(coat!1).L().CLTC(coat!1).L() kon_cltc_cltc_pm
	
	CLTC(epn1) + EPN1(coat) <-> CLTC(epn1!1).EPN1(coat!1) kon_cltc_epn1_cp, koff
	CLTC(epn1).L() + EPN1(coat).L() -> CLTC(epn1!1).L().EPN1(coat!1).L() kon_cltc_epn1_pm
	
	EPN1(L) + L(p) <-> EPN1(L!1).L(p!1) kon_epn1_l_cp, koff
	EPN1(L).L() + L(p) -> EPN1(L!1).L(p!1).L() kon_epn1_l_pm

end reaction rules

end model

simulate_nf({suffix=>"nf",get_final_state=>1,t_end=>10,n_steps=>2500,param=>"-gml 1000000 -v"});
